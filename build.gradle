buildscript {
  ext {
    indraVersion = '2.0.6'
    nmsMapperVersion = '1.0.5'
  }
}

plugins {
  id 'net.kyori.indra' version "$indraVersion" apply false
  id 'net.kyori.indra.publishing.sonatype' version "$indraVersion"
  id 'net.kyori.indra.license-header' version "$indraVersion" apply false
  id 'org.screamingsandals.nms-mapper' version "$nmsMapperVersion" apply false
}

// Adventure version
ext.adventure = "4.8.1"

group 'org.screamingsandals'
version '4.0.0-SNAPSHOT'
description 'Platform integrations for the adventure UI library'


/**
 * Bukkit and its dependencies have metadata that is incorrect and excludes some annotation APIs.
 *
 * To suppress compiler warnings from being unable to find the appropriate annotations,
 * we inject the dependencies here.
 */
@CacheableRule
class BukkitAnnotationsRule implements ComponentMetadataRule {

  @Override
  void execute(final ComponentMetadataContext componentMetadataContext) {
    componentMetadataContext.details.withVariant("compile") {
      withDependencies {
        add("com.google.errorprone:error_prone_annotations:2.4.0")
        add("org.jetbrains:annotations:20.1.0")
      }
    }
  }
}

subprojects {
  if (project.name == 'adventure-platform-nms') {
    return
  }

  apply plugin: 'net.kyori.indra'
  apply plugin: 'net.kyori.indra.publishing'
  //apply plugin: 'net.kyori.indra.checkstyle'
  apply plugin: 'net.kyori.indra.license-header'

  repositories {
    mavenCentral()
    sonatypeSnapshots()
    maven {
      name = 'viaversion'
      url = 'https://repo.viaversion.com'
    }
  }

  dependencies {
    components {
      withModule("com.destroystokyo.paper:paper-api", BukkitAnnotationsRule)
    }
    //checkstyle 'ca.stellardrift:stylecheck:0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
  }

  indra {
    javaVersions {
      testWith(8, 11, 16)
    }

    github("KyoriPowered", "adventure-platform") {
      ci(true)
    }
    mitLicense()

    configurePublications {
      pom {
        developers {
          developer {
            id = 'kashike'
            timezone = 'America/Vancouver'
          }

          developer {
            id = 'lucko'
            name = 'Luck'
            url = 'https://lucko.me'
            email = 'git@lucko.me'
          }

          developer {
            id = 'zml'
            name = 'zml'
            timezone = 'America/Vancouver'
          }

          developer {
            id = 'Electroid'
          }
        }
      }
    }
  }
}
